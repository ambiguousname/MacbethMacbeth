VAR selfishEnemy = ""
VAR selfishDistinctOutcome = ""
VAR selfishReplacedByFakeMacbeth = false
VAR selfishMacduffReplacedByOphelia = false

=== selfish_intro ===
\*SEYTON\* peers out from stage right.

SEYTON (Whispering): AHEM! Bring me no reports; let them fly all.

* MACBETH: What was that?
    SEYTON (Loudly Whispering): Bring me no reports! Let them fly all!
    ** MACBETH (Shouting): WHAT?
        SEYTON (Shouting): BRING ME NO REPORTS! LET THEM FLY ALL!
        *** MACBETH: Hey, keep it down!
            SEYTON (Shouting): HAVE YOU EVER SEEN AN ACTOR STRANGLE SOMEONE BEFORE?
            
            \*SEYTON turns to look offstage.\*
            
            SEYTON: What? WHAT? Macbeth started it!
            
            \*SERVANT clears their throat.\*
            
            SERVANT: There is ten thousand--
            
            **** MACBETH: Ten thousand what?
                SERVANT: Geese. Wait, no... err...
                
                ***** MACBETH: Ten thousand geese?
                    SERVANT: No. Soldiers, sir!
                    ****** MACBETH: Is that how many soldiers I have?
                        SERVANT: Go prick thy face, and over-red thy fear,
                        
                        ~ selfishEnemy = "SERVANT"
                        
                        ******* MACBETH: What did you just say to me?
                            SERVANT: Wait! WAIT! The English force, so please you.
                            
                            \*SEYTON enters from stage right, slowly clapping.\*
                            
                            SEYTON: Congrats, congrats! I have your armour here Sire, and LOOK, MACDUFF!
                            \*MACBETH, SEYTON, DOCTOR, and SERVANT exit.\*
                        ******* MACBETH: My fear hath never been red.
                            SERVANT: Wait, wait, that's err... that's wrong.
                            It were, uhh, I mean... It is, it is good... no,
                            it's uhhh, it is bad... 
                            
                            \*The SERVANT's mumblings grow quieter and quieter.\*
                            
                            DOCTOR: Ha, your royal preparation is ready! Let us go!
                            
                            \*MACBETH, DOCTOR, and SERVANT exit.\*
                    ****** MACBETH: That's no good.
                    
                        LIST possibleEnemies = MACDUFF, DOCTOR, SERVANT, SEYTON
                        ~ selfishEnemy = "{LIST_RANDOM(possibleEnemies)}"
                    
                        SERVANT: No. It's... not.
                        
                        \*There is an uncomfortable silence as the SERVANT and MACBETH stare at each other for quite some time.\*
                        
                        DOCTOR: Ha, I am going to... leave now. And MACBETH will come with me to... check on things.
                        
                        \*SERVANT, MACBETH, and DOCTOR exit.\*
                        
                ***** MACBETH: Wait, what happened to my sick wife?
                    ~ selfishEnemy = "DOCTOR"
                
                    DOCTOR: Ha, yes! She is troubled with thick coming fancies,
                    That keep her from her rest.
                    
                    ****** MACBETH: Whatever you said makes no sense.
                        DOCTOR: Ha, very good. My good lord! Ha, your... royal preparation
                        Makes us hear something.
                        
                        *******[MACBETH: I still am not making any sense of what you're saying.]
                        *******[MACBETH: I will make you unhear it.]
                        *******[MACBETH: I still don't know my lines.]
                        
                        -------
                        MACBETH: I--
                        
                        DOCTOR: HA YES, VERY GOOD! WERE I FROM DUNSINANE! I mean...
                        
                        DOCTOR (Aside): Were I from Dunsinane away and clear,
                        Profit again should hardly draw me here.
                        
                        \*DOCTOR rushes over to MACBETH to force them both offstage. SERVANT follows.\*
            
            **** \*MACBETH pauses to look at the SERVANT\*
                SERVANT: What are you... doing?
                
                ~selfishEnemy = "SERVANT"
                
                ***** \*MACBETH continues to stare.\*
                    SERVANT: Please... stop?
                    ****** MACBETH: IT WAS YOU!
                        SERVANT: What?
                        
                        \*The PLAY DIRECTOR enters.\*
                        
                        PLAY DIRECTOR: Uhm... Shock! Surprise! The SERVANT killed...
                        
                        ******* MACBETH: My wife! You poisoned my wife!
                            PLAY DIRECTOR: Sure, let's go with that.
                            
                            \*The PLAY DIRECTOR waits for a few seconds.\*
                            
                            PLAY DIRECTOR: AND so, MACBETH went offstage to... deal with the SERVANT!
                            
                            \*MACBETH, SERVANT, DOCTOR, and PLAY DIRECTOR exit.\*
                            
                            ~selfishDistinctOutcome = "ServantPoisonedWife"
                        ******* MACBETH: The king! You killed the poor king!
                            SERVANT: I killed the king?
                            
                            PLAY DIRECTOR: What? No!
                            
                            ******** \*MACBETH exits.\*
                            
                            PLAY DIRECTOR: Hey, wait a minute!
                            
                            \*SERVANT, DOCTOR, and PLAY DIRECTOR exit, chasing after MACBETH.\*
                            
                            ~selfishDistinctOutcome = "ServantKilledKing"
                    ****** MACBETH: No.
                        SERVANT: Please...?
                        
                        \*The DOCTOR quickly steps up to the SERVANT.\*
                        
                        DOCTOR: I think sire Macbeth wishes no company. Let us depart.
                        
                        \*SERVANT exits. DOCTOR is about to exit, and turns to face MACBETH. MACBETH stares at DOCTOR silently.\*
                        
                        DOCTOR: Seyton shall be with your armour, lord. Ha, I shall do my best to cure my patient.
                        
                        \*The DOCTOR tugs MACBETH offstage.\*
                ***** MACBETH: I don't know.
                    SERVANT: Okay... I feel like... I mean, I should probably...
                    
                    \*FAKE MACBETH peers out onstage.\*
                    
                    FAKE MACBETH: Alright, I'm gonna have a go. Excuse me, excuse me.
                    
                    \*MACBETH is hurriedly escorted offstage. FAKE MACBETH performs the rest of the scene, and exits with DOCTOR and SERVANT.\*
                    
                    ~selfishReplacedByFakeMacbeth = true
            **** MACBETH: Don't think we're not done, Seyton.
                ~selfishEnemy = "SEYTON"
            
                SEYTON (Shouting): MACBETH! KEEP YOUR FLYING TRAP SHUT! THIS IS THE LAST STRAW!
                        
                    \*SEYTON marches on stage. PLAY DIRECTOR follows.\*
                    
                    PLAY DIRECTOR: Look, please don't make a scene...
                    
                    SEYTON: Oh, oh, I'M NOT MAKING A SCENE? WHAT DO YOU CALL THIS?
                    
                    \*SEYTON gestures wildly at MACBETH.\*
                    
                    PLAY DIRECTOR: Scene III. Now get off the stage.
                    
                    ***** MACBETH: Listen, I mean no disrespect...
                        SEYTON: Sure, like I believe you.
                        
                        \*PLAY DIRECTOR clears their throat.\*
                        
                        PLAY DIRECTOR: And yes, so MACBETH got into an argument with SEYTON, and MACBETH 'twere very sad.
                        
                        \*PLAY DIRECTOR escorts SEYTON, MACBETH, DOCTOR, and SERVANT offstage.\*
                    ***** MACBETH: Got something to say to me, Seyton?
                        SEYTON: Oh yeah, I've got two somethings you little...
                        
                        \*PLAY DIRECTOR clears their throat.\*
                        
                        PLAY DIRECTOR: And yes, so MACBETH got into an argument with SEYTON, and MACBETH 'twere very sad.
                        
                        \*PLAY DIRECTOR escorts SEYTON, MACBETH, DOCTOR, and SERVANT offstage.\*
            
        *** MACBETH: Ah, yes. Bring me no reports, let them fly all;
            **** MACBETH: Okay, that's it. See you all later!
                ~selfishDistinctOutcome = "LeftEarly"
                \*MACBETH begins to bow, and exits.\*
            **** MACBETH: Next line?
                SEYTON (Shouting): TILL BIRNAM WOOD REMOVE TO DUNSINANE,
                    ***** MACBETH: Till Birnam wood remove to Dunsinane,
                        SEYTON: (Shouting): I CANNOT TAINT WITH FEAR.
                        
                        \*The AUDIENCE is visibly annoyed. HECKLER shouts from offstage.\*
                        
                        HECKLER: KNOW YOUR LINES, IDIOT! WE CAN ALL HEAR YOU!
                        
                        
                        \*FAKE MACBETH enters.\*
                        
                        FAKE MACBETH: I think it's best if I handle this. Sorry, Macbeth.
                        
                        \*MACBETH exits, and FAKE MACBETH plays out the rest of the scene.\*
                        
                        ~selfishDistinctOutcome = "Heckled"
                        ~selfishReplacedByFakeMacbeth = true
                        
                    ***** MACBETH: How many more lines am I going to have?
                        SEYTON (Shouting): TILL BIRNAM WOOD REMOVE TO DUNSINANE,
                        ****** MACBETH: Sorry, are you broken?
                            ~selfishEnemy = "SEYTON"
                            SEYTON: I BREAK YOUR FACE!
                            
                            \*SEYTON leaps towards MACBETH, and drags them offstage.\*
                        ****** MACBETH: Hey wait, what's that?
                            ->witch_encounter->
        
    ** MACBETH: I mean, I can't understand what you're saying.
        SEYTON (Whispering): It means Macbeth doesn't want to hear any bad news.
        *** MACBETH: I get that, but... wait, why doesn't Macbeth want to hear any bad news?
            ~selfishEnemy = "SEYTON"
        
            SEYTON (No longer whispering): Because he killed King Duncan, and he's now regretting the consequences of his actions!
            **** MACBETH: Wait, Macbeth's a murderer?
                \*SEYTON steps onto the stage, and approaches MACBETH.\*
                SEYTON (Whispering to MACBETH): You are a thespian, right?
                
                \*SEYTON steps back and clears his throat.\*
                
                SEYTON: What is your gracious pleasure?
                
                ***** MACBETH: If I'm a murderer, I guess I can just turn myself into the police, right?
                    SEYTON: Uhhh, police don't exist yet?
                    
                    SERVANT: There's the English army!
                    
                    SEYTON: Great, we can surrender Macbeth to them. Let's go.
                    
                    \*SEYTON forces MACBETH offstage, with SERVANT and DOCTOR following.\*
                    
                    ~selfishDistinctOutcome = "SurrenderingToTheEnglish"
                ***** MACBETH: Can I just be king?
                    SEYTON: You... you are king.
                
                    ****** MACBETH: Yeah, but the army is showing up, right?
                        SERVANT: The English army, yes.
                        
                        DOCTOR: Ha, and your wife is still sick.
                        
                        ******* MACBETH: Let's go kill the army, then.
                            \*MACBETH, DOCTOR, SERVANT, and SEYTON exit.\*
                        ******* MACBETH: Let's sit here and wait until they come to kill us.
                            SEYTON: Uhh, no. No. We need to exit. Now.
                            \*SEYTON forces MACBETH, SERVANT, and DOCTOR offstage.\*
                            
                            ~selfishDistinctOutcome = "SittingForDeath"
                    ****** MACBETH: I wanna be... a SUPER king.
                        SEYTON: That's not a real title!
                        
                        SEYTON: And besides... the army is showing up. To forcibly remove you.
                        
                        ******* MACBETH: I'm not too worried about that.
                            SEYTON: Ahh, because of the prophecy?
                            
                            ******** MACBETH: There's a prophecy?
                                SEYTON: YES! AND LET ME...
                                
                                \*SEYTON looks towards the AUDIENCE.\*
                                
                                SEYTON: And let me show you it... offstage.
                                
                                \*MACBETH, SEYTON, DOCTOR, and SERVANT exit.\*
                            ******** MACBETH: Yes, the prophecy!
                                SEYTON: Great! You know the prophecy. Let's bounce.
                                
                                \*MACBETH, SEYTON, DOCTOR, and SERVANT exit.\*
                        ******* MACBETH: Well, do something about it!
                            SEYTON: I'll... go fetch your armour.
                            
                            \*SEYTON leaves, and then doubles back.\*
                            
                            SEYTON: Actually, you should come and get it with me! That would be much better for the production. I mean... the battle! You should get things... set up for yourself.
                            
                            \*SEYTON, MACBETH, DOCTOR, and SERVANT exit.\*
                ***** MACBETH: Hmm... I dunno. 
                    SEYTON: Great, you're really giving it your all, Macbeth.
                    
                    \*SEYTON pauses.\*
                    
                    SEYTON: I mean, here I am, I showed up on time. To the play. The play that you're supposed to show up on time for.
                    
                    \*SEYTON pauses.\*
                    
                    SEYTON: The play that you're late for. And you don't know your lines. "That's fine", I think, and I try to help you.
                    
                    \*SEYTON pauses.\*
                    
                    SEYTON: And then, you go off the rails. You say whatever comes to mind. You do your own thing.
                    
                    \*SEYTON pauses.\*
                    
                    SEYTON: And you're really showcasing your A-Grade acting material here. You're really selling me on MACBETH's dilemma here.
                    
                    \*SEYTON pauses.\*
                    
                    SEYTON: You're really showcasing the caverns of...
                    
                    \*SEYTON pauses.\*
                    
                    SEYTON: Vast.
                    
                    \*SEYTON pauses.\*
                    
                    SEYTON: Deep.
                    
                    \*SEYTON pauses.\*
                    
                    SEYTON: Bottomless.
                    
                    \*SEYTON pauses.\*
                    
                    SEYTON: Intelligence and Ability that you have available. So why don't you do me a favor, and...
                    
                    \*SEYTON pauses.\*
                    
                    SEYTON (Shouting): GET OFF THE STAGE!
                    
                    \*SEYTON drags MACBETH offstage. The DOCTOR and the SERVANT follow.\*
                    
                    ~selfishDistinctOutcome = "ReallyUpsetSeyton"
            **** MACBETH: Serves Macbeth right then. Guess I shouldn't've killed the King.
                SEYTON: Yes, I guess he shouldn't've. Lesson learned.
                
                DOCTOR: But, I have my whole...
                
                SEYTON: Let's just move onto the next scene before things get worse.
                
                \*DOCTOR, SERVANT, and MACBETH exit.\*
        *** MACBETH: Sorry, say that again? I didn't catch all of that.
            SEYTON (Loudly Whispering): Are you kidding me?
            
            **** MACBETH: Don't worry, I'll make up a new motivation.
                SEYTON: What? Uhhh, no! Don't do that!
                
                ~selfishEnemy = "MACDUFF"
                
                ***** MACBETH: Macduff killed my father.
                    ~selfishDistinctOutcome = "MacduffMurderedFather"
                ***** MACBETH: Macduff was the one who really killed the king!
                    ~selfishDistinctOutcome = "MacduffKilledKing"
                -----
                SEYTON: No! No he didn't! This didn't happen!
                
                \*MACBETH, DOCTOR, and SERVANT exit.\*
* MACBETH: I think that could do with some revision.
    \*MACDUFF leans out from stage left.\*
    MACDUFF (Whispering): Don't get fancy with it, just stick to the script.
    
    ~selfishEnemy = "MACDUFF"
    ** MACBETH: Ha, I didn't read the script.
        MACDUFF (Whispering): I am shocked. Shocked, that you cannot read.
        
        *** MACBETH: You don't have to be such a jerk, Macduff.
            MACDUFF: The way you phrase it, it's almost like I'm not your antagonist. Like I'm not meant to be the actual foil to your diabolical plans.
            
            **** MACBETH: Diabolical plans?
                MACDUFF: Diabolical, yes. You killed the king. You know what, I can't discuss this with you. I'll see you in a few scenes.
                
                \*MACDUFF yanks MACBETH offstage. The SERVANT and the DOCTOR follow.\*
            **** MACBETH: Alright, if you're my antagonist... GUARDS!
                MACDUFF: What?
                
                \*A few GUARDS enter, escorting MACDUFF onstage.\*
                ***** MACBETH: Schedule Macduff's execution.
                    MACDUFF: Wait a second, you can't do that.
                    
                    \*A GUARD grabs MACDUFF.\*
                    
                    MACDUFF: Hey, hold on, wait one second. This isn't in the script! I'm offstage! You can't...
                    
                    \*MACDUFF's cries grow quieter as they are dragged offstage.\*
                    
                    MACDUFF: Wait! I still need to do the next scene, you idiots!
                    
                    ****** MACBETH: Fine, but we're just going to execute you after that.
                        MACDUFF: Great. See you after.
                        
                        \*The GUARDS, MACDUFF, MACBETH, the SERVANT, and the DOCTOR exit.\*
                        
                        ~selfishDistinctOutcome = "MacduffExecutionScheduled"
                    ****** MACBETH: Sorry, no can do. I'll just replace you with an impostor.
                        \*FAKE MACBETH enters.\*
                        
                        FAKE MACBETH: I guess I'm Macduff now.
                        
                        (FAKE) MACDUFF: You can call me Macduff for this next scene.
                        
                        \*The GUARDS, MACDUFF, FAKE MACBETH (To be MACDUFF next scene), MACBETH, the SERVANT, and the DOCTOR exit.\*
                        
                        ~selfishDistinctOutcome = "MacduffExecutionScheduledImpostor"
                ***** MACBETH: I dunno, bind Macduff's hands together or something.
                    \*A GUARD binds MACDUFF's hands together.\*
                    
                    MACDUFF: Great. Now my hands are bound together. I'll see you in a bit.
                    
                    \*The GUARDS, MACDUFF, MACBETH, the SERVANT, and the DOCTOR exit.\*
                    ~selfishDistinctOutcome = "MacduffHandsBound"
        *** MACBETH: Wait, if I can't read, how can I read this?
            -> witch_encounter ->
    ** MACBETH: Stick with the script. Got it.
        MACDUFF (Whispering): Good.
        
        *** MACBETH: And that's why your honor, by habeas corpus, my client is completely innocent!
            MACDUFF (Whispering): What are you doing?
            
            PLAY DIRECTOR (Offstage): Is someone modifying the script?
            
            SEYTON (Offstage): It's Macbeth.
            
            MACDUFF (Whispering): You realize I have a law degree, right? And what you said is complete nonsense.
            
            PLAY DIRECTOR (Offstage): Okay, get the understudy back on.
            
            \*MACBETH is yanked offstage. The SERVANT and the DOCTOR follow.\*
            ~selfishReplacedByFakeMacbeth = true
        *** MACBETH: Wait, I still don't know the script.
            SEYTON (Whispering): I just fed you your lines!
            
            MACDUFF (Whispering): No revisions, stick to the script!
            
            **** MACBETH: But it's really hard to scroll all the way up and read whatever you said.
                SEYTON (Whispering): What are you talking about?
                
                MACDUFF (Whispering): Just say the line again so Macbeth can say it.
                
                SEYTON (Whispering): What?
                
                MACDUFF (Whispering): Can you speak up? I can't hear you.
                
                ***** MACBETH: Just both speak up at the same time!
                    SEYTON: I am TRYING to keep the illusion that we are professionals!
                    
                    \*SEYTON pauses.\*
                    
                    SEYTON: I mean, that you are still a professional! Because I certainly am one.
                    
                    MACDUFF (Whispering): Keep it down.
                    
                    SEYTON: WHAT? I can't HEAR you!
                    
                    MACDUFF (Whispering): Oh this is pointless.
                
                    SEYTON: I give up. Let's just skip to a scene where someone knows their lines!
                    
                    \*MACBETH is yanked offstage. The SERVANT and the DOCTOR follow.\*
                ***** MACBETH (Whispering): I can whisper too.
                    SEYTON (Whispering): [Unintelligble]
                    
                    MACDUFF (Whispering): [Unintelligble]
                    
                    SEYTON (Whispering): [Unintelligble]
                    
                    MACDUFF (Whispering): [Unintelligble]
                    
                    \*MACDUFF grabs MACBETH and drags them offstage.\*
            **** MACBETH: But what if this is the script?
                SEYTON: Okay, I think we're done here. Macduff, you're up.
                
                \*MACBETH is forcefully moved offstage. The SERVANT and the DOCTOR follow.\*
-
->->

=== witch_encounter ===
~selfishEnemy = "Macduff"

\*FIRST WITCH, SECOND WITCH, and THIRD WITCH pop up from underneath the stage.\*
FIRST WITCH: All hail Macbeth! Hail to thee, thane of delusion!

SECOND WITCH: All hail Macbeth! Hail to thee, thane of lies!

THIRD WITCH: Macbeth has already heard this part! Probably. Let's skip to the good stuff!

FIRST WITCH: We must call for a ritual!

SECOND WITCH: Yes a ritual! But what of the stage? What of Macbeth?

THIRD WITCH: Indeed, the AUDIENCE must not be kept waiting!

FIRST WITCH: Permit us to spirit you to safety! To bind your extremities, to unravel your orifices!

* MACBETH: What are you talking about?
* MACBETH: No way!
* MACBETH: I permit it.
    SECOND WITCH: HA! Macbeth permits us to... bind them.
    
    THIRD WITCH: Prepare the rope!
    
    FIRST WITCH: I call the legs!
    
    \*FIRST WITCH, SECOND WITCH, and THIRD WITCH cackle gleefully, and begin to bind MACBETH.\*
    
    ALL: Show thy eyes, grieve thy heart;
    With your soul entwined, we so depart!
    
    FIRST WITCH: When you seek our host,
    take your line-d path upmost!
    
    \*FIRST WITCH, SECOND WITCH, THIRD WITCH, and MACBETH exit.\*
    ->->
-
SECOND WITCH: Macbeth rejects our gifts. Foolish, spiteful, scoliotic conduct.

THIRD WITCH: Fare thee well,

FIRST WITCH: We see you off!

SECOND WITCH: Remember: when all in slough,

THIRD WITCH: And the winds blow rough,

FIRST WITCH: Take great warning,

ALL: AND BEWARE MACDUFF!

\*FIRST WITCH, SECOND WITCH, and THIRD WITCH vanish. MACDUFF enters.\*

MACDUFF (Hurriedly Whispering): Why have you been staring at the stage for the past five minutes? We already had your understudy do the lines already...

\*MACBETH stares at the AUDIENCE. The AUDIENCE does not look particularly amused.\*

MACDUFF (Aggressively Whispering Through Clenched Teeth): GET. OFF. THE. STAGE.

\*MACBETH exits.\*
->->

/*
 These arguments are incredibly stupid, but there is no toUpperCase in Ink (not that I can find): 
*/

=== function print_enemy(firstLetter, firstLetterCapital)
    {selfishEnemy:
    - "DOCTOR":
        ~ return selfishEnemy + ": Ha, " + firstLetter
    - "":
        ~ return "SEYTON: " + firstLetter
    - else:
        ~ return selfishEnemy + ": " + firstLetterCapital
    }
=== selfish_middle ====
{ selfishDistinctOutcome:
- "ServantPoisonedWife":
- "ServantKilledKing":
    -> servant_murderer_outcome ->
- "LeftEarly":
    -> left_early_outcome ->
- "Heckled":
    -> heckled_outcome ->
- "SurrenderingToTheEnglish":
    -> surrendering_outcome ->
- "SittingForDeath":
    -> waiting_for_death_outcome ->
- "ReallyUpsetSeyton":
    ->upset_seyton_outcome->
- "MacduffExecutionScheduled":
- "MacduffExecutionScheduledImpostor":
    -> macduff_execution_outcome ->
- "":
    {selfishEnemy == "":
        SEYTON: Not this again.
    -else:
        {selfishEnemy != "SEYTON": 
        \*{selfishEnemy} enters.\*
        }
        {print_enemy("n", "N")}ot this again. {selfishEnemy == "SERVANT": Please... please... just say the line right!| Just get off the stage already, nincompoop!}
    }
    
    -> selfish_middle_no_distinct ->
    
    
}
// Macduff hands bound will only be used for finale (when Macbeth fights Macduff).
// Same with the whole "Macduff killed my father" or "Macduff killed the king". That will be saved for the duel with Macduff.

->->

=== selfish_middle_no_distinct ===
* MACBETH: Oh, boo-hoo. I don't know my lines. I can always make something up.
    {print_enemy("o", "O")}h no. Please don't make anything up.
    
    ** MACBETH: MACBETH!
        {selfishEnemy == "SEYTON":
            SEYTON: What are you DOING? You can't say that name! You'll curse the whole production!
        - else: 
            {print_enemy("w", "W")}hat are you doing?
            
            \*PLAY DIRECTOR enters.\*
            
            PLAY DIRECTOR: Why are you going off-script{selfish_intro: again}? And... wait a second, are you invoking the Macbeth name out of context?
        }
        
        ~ temp witchReady = false
        
        *** MACBETH: What are you talking about?
            ~witchReady = true
        *** MACBETH: I'm improvising.
            {selfishEnemy == "SEYTON": 
                SEYTON: Well, stop it!
            - else:
                PLAY DIRECTOR: Stop it, you lily-guilded rat face!
            }
            
        ---
            {selfishEnemy == "SEYTON":
                SEYTON: Everyone knows Shakespeare wrote Macbeth using real witches. As long as we don't anger them, we'll be fine. Unless you start doing stupid crap like this! STAY ON SCRIPT!
            - else: 
                PLAY DIRECTOR: Do you realize just how many actors have been crushed during productions of Macbeth? And you're about to be one of them, unless you shut your stupid face!
            }
            
            **** MACBETH: I don't believe in that superstitious nonsense.
                {witchReady && witch_encounter:
                    \*FIRST WITCH, SECOND WITCH, and THIRD WITCH enter.\*
                    
                    FIRST WITCH: Congratulations on making it this far!
                    
                    SECOND WITCH: We enjoyed binding your extremities greatly!
                    
                    THIRD WITCH: And now, your reward!
                    
                    ALL: The Script!
                    
                    \*The FIRST WITCH hands MACBETH this script. MACBETH reads the script.\*
                    
                    MACBETH: Hey wait a second, this is where we are right now! It says that I'm supposed to read the script!
                    
                    FIRST WITCH: And now you can change it to be whatever you want!
                    
                    SECOND WITCH: Not if we're puppetting the poor soul. Let me see...
                    
                    \*The SECOND WITCH commands MACBETH to rewrite the script.\*
                    
                    CAPTAIN UNKILLABLE: Hey, what did you just do?
                    
                    SKELEDUDE: And laser beam... GO!
                    
                    CAPTAIN UNKILLABLE: What?
                    
                    \*The laser kills CAPTAIN UNKILLABLE.\*
                    
                    SKELEDUDE: Man, what an idiot. Glad I was delivered by C-Section, otherwise that would have never worked.
                    
                    MINION: What?
                    
                    SKELEDUDE: Nevermind. Let's go dominate the universe or whatever.
                    
                    The End.
                    ->END
                -else: 
                    ->crushed_under->
                }
            **** MACBETH: MACBETH! MACBETH! MACBETH!
                -> crushed_under ->
    ** MACBETH: I've got it! The English army is... disguised as... a bunch of trees! Go kill them!
        {print_enemy("w", "W")}hat? Hey, you're not supposed to know that yet!
        
        FIRST SOLDIER: And it doesn't really make a difference. We're still going to die to the army, even though we still know that they're disguised with branches.
        
        SECOND SOLDIER: Speaking of, here they come to kill us now!
        
        {selfishEnemy == "SEYTON" || selfishEnemy == "": 
        \*MACBETH, SEYTON, and the SOLDIERS exit.\*
        -else:
        \*MACBETH, SEYTON, {selfishEnemy}, and the SOLDIERS exit.\*
        }
* MACBETH: I seriously don't know the line.
    {selfishReplacedByFakeMacbeth:
        PLAY DIRECTOR (Offstage): Alright, we'll just get your understudy to replace you, same as last time.
        
        \*MACBETH is yanked offstage and replaced by FAKE MACBETH. FAKE MACBETH learns LADY MACBETH is dead, and prepares for war. FAKE MACBETH, SEYTON, and the SOLDIERS exit.\*
    -else:
        ->dont_know_line_middle->
    }
-
->->

=== dont_know_line_middle ===
{print_enemy("o", "O")}kay, what do you actually know?
        
* MACBETH: I don't know. Let's just go to the next scene and see how this all ends.
    {print_enemy("t", "T")}hat's probably your first good idea of the night.
* MACBETH: EVERYTHING! I AM OMNIPOTENT!
    {selfishEnemy == "MACDUFF":
        MACDUFF: You don't even know my army is coming to kill you.
    - else:
        {print_enemy("y", "Y")}ou don't even know that an army is coming to kill you disguised as branches!
    }
    
    {print_enemy("t", "T")}his was a bad idea.
    
    {selfishEnemy == "SEYTON" || selfishEnemy == "MACDUFF":
        \*{selfishEnemy} turns to the AUDIENCE.\*
        {print_enemy("l", "L")}isten folks, all that really happens is that Macbeth learns more people are dead, and more people are about to die.
    }

-
{selfishEnemy == "SEYTON" || selfishEnemy == "": 
\*MACBETH, SEYTON, and the SOLDIERS exit.\*
-else:
\*MACBETH, SEYTON, {selfishEnemy}, and the SOLDIERS exit.\*
}
->->

=== crushed_under ===
\*A stage light lands directly on MACBETH.\*

{selfishEnemy == "SEYTON":
SEYTON: I am shocked. SHOCKED! Serves you right.
-else:
PLAY DIRECTOR: Told you.
}

* [MACBETH: Help, I'm still alive!]
* [MACBETH: You think a puny stage light can stop Macbeth?]

-
\*MACBETH grumbles incoherent angry nonsense underneath the stage light.\*

{selfishEnemy == "SEYTON":
SEYTON: Oh ho ho, how the wheel turns. Not too pleased with yourself, are you now?
-else:
PLAY DIRECTOR: Oh, I guess you're still under there. I think we can act around you.
}

\*The actors continue out the rest of the scene, pretending that MACBETH is in fact, not trapped under a stage light. They also pretend that MACBETH is a better actor than reality might indicate. All exit, except for MACBETH (who is still trapped under the stage light).\*

->->

=== servant_murderer_outcome ===
SEYTON: Alright, I guess... we're continuing our conversation from last time, since the Servant is apparently a murderer.

\*SERVANT enters.\*

SERVANT: I am not!

SEYTON: Okay, well, it doesn't make any sense NOT to follow this through line that Macbeth has so generously started for us.

SERVANT: Seyton please, I only got to say a few things, and besides, why worry about my death when there's an army at the gate?

SEYTON: Pretty sure that even as a King who got power through murder, Macbeth has to execute murderers.

SERVANT: But I didn't even do it!

SEYTON: Save it for... wait, does anyone hear that?

SERVANT: Hear what?

SEYTON: It's like a low humming.

SERVANT: Wait, wait, I think I hear it too.

\*The DOCTOR runs onstage.\*

DOCTOR (Shouting): BEES!

\*A box of bees is thrown onto the stage. Swarms and swarms of Bees exit from the box and begin to sting all present. Please procure real, angry bees for the illusion of being stung by bees to work. You'll know you're doing a good job if the AUDIENCE is screaming in terror (Things like "OW! OW! WHY DID YOU INTRODUCE REAL, LIVE BEES!" or "I'M ALLERGIC! SOMEONE CALL AN AMBULANCE!").\*

DOCTOR (Screaming): THIS HURTS! THIS HURTS SO MUCH! I WISH I WERE A REAL DOCTOR!

SEYTON (Screaming): MACBETH! DO SOMETHING!

* MACBETH: I can introduce the bees' natural predator: WASPS!
    SERVANT (Screaming): I TAKE IT ALL BACK! I KILLED EVERYONE! JUST DON'T--!
    
    \*MACBETH throws a box of wasps onstage. Again, it is imperative you acquire real, angry wasps. Other poisonous insects are also encouraged.\*
    
    DOCTOR (Screaming): NO! NO! NOOOOOOOOOOOOOOOO! PLEASE, STAY AWAY! AGHHHHH!
    
    SEYTON (Screaming): SOMEONE JUST CUT TO THE NEXT SCENE ALREADY!
    
    \*PLAY DIRECTOR, if they are still conscious, enters.\*
    
    PLAY DIRECTOR (Screaming): WE CAN'T! THE BEES AND THE WASPS ARE ALL OVER THE STAGE!
    
    \*MACDUFF enters.\*
    
    MACDUFF (Screaming): I HAVE AN IDEA! DID ANYONE BRING ANY MATCHES? WE COULD BURN THE THEATRE DOWN!
    
    SEYTON (Screaming): WAIT! WAIT! OW! OW! I STILL HAVE YET TO BE RECOGNIZED FOR MY PERFORMANCE! AND, OW! I HAVE A BETTER IDEA! EVERYONE RUN OUTSIDE! INTO THE RAIN!
    
    \*All run out of the theatre towards rain. At this point in the play, please activate your supersonic BEE and WASP neutralizer included with this script. In the case that such a device is missing, please continue the play as if you have removed all the BEES and WASPS.\*
    
    SEYTON (Huffing): Okay. Okay. Everyone back inside!
    
    \*All but MACDUFF run back inside. The AUDIENCE is mostly gone at this point.\*
    
    SEYTON: Okay, let's just skip to Scene VI, and... wait, where's Macduff?
    
    PLAY DIRECTOR: I don't know.
    
    MACDUFF (Offstage): Listen, this has been great, but I need to take up a much less dangerous hobby! You can have Macbeth do my parts!
    
    \*All faces at this point should be considerably swollen.\*
    
    SEYTON: No, NO!
    
    PLAY DIRECTOR: Well, it wouldn't make any sense to have MACBETH do the next scene as MACDUFF, but I don't see why not for the fight scene.
    
    SEYTON: What are you TALKING about?
    
    PLAY DIRECTOR: We'll just pretend MACDUFF is present for the next scene.
    
    SEYTON: That's NOT what I mean!
    
* MACBETH: Everyone, run for your lives!
    SEYTON (Screaming): GOOD IDEA! EVERYONE RUNNN!!!!!!
    
    \*All run out of the theatre towards the rain. Please call for a BEE exterminator if you have not done so. Once everyone has calmed down, force all ACTORs and AUDIENCE members back into the Theatre. Threaten BEE extermination if they do not comply.\*
    
    SEYTON: Okay, I guess... let's just pick up from where we left off?
    
    \*All faces at this point should be considerably swollen.\*
    
    MACDUFF: Let's just skip to my scene, where there will hopefully be considerably less bees.
    
    DOCTOR (Offstage): Ha, bEE!
    
    MACDUFF: SHUT. IT.
    
    \*All exit.\*
-
->->

=== left_early_outcome ===
SEYTON: Oh, screw this.

\*SEYTON grabs MACBETH's face, and ties it down to a stick using some rope. SEYTON pulls out a chair and sits down on it.\*

SEYTON: Well, Macbeth, how do you feel about the war?

MACBETH (Ventriloquized by SEYTON): I'm feeling pretty bad, dude. These English are tryin' to go HAM on me, and I'm just not feeling it.

SEYTON: Well, there's even worse news for you yet.

\*A cry comes from within the castle.\*

MACBETH (Ventriloquized by SEYTON): What was that?

SEYTON: Your wife's dead, dummy.

MACBETH (Ventriloquized by SEYTON): I'm not the dummy, you're the dummy!

SEYTON: That doesn't make any sense.

MACBETH (Ventriloquized by SEYTON): Well, you're the one that's making the jokes. And everyone can see your lips move!

SEYTON: CANNOT!

MACBETH (Ventriloquized by SEYTON): Can too!

SEYTON: That's IT, you little creep!

\*SEYTON reaches for MACBETH's head.\*

* MACBETH: Someone help me!
    \*No one steps in to help. Seyton inches their face closer to MACBETH's face.\*
    
    SEYTON: No one's coming to help you, you pathetic excuse of an actor.
    Give up midway through the play, will you? Start your soliloquy only to end it?
    This is my big break. My shot at stardom. Granted, Seyton only shows up in Act V,
    but I was really going to NAIL this. So I'm going to keep nailing it, until the PLAY IS FINISHED.
    NOW STAY STILL!
    
    \*SEYTON finishes the rest of the scene. Whatever, not like you have a choice as to what you say.\*
* MACBETH: Wow, what a class act!
    \*The AUDIENCE is completely silent. SEYTON begins to laugh.\*
    
    MACBETH (Ventriloquized by SEYTON): Wow, what a great act!
    
    \*SEYTON continues to laugh, increasing with intensity.\*
    
    MACETH (Ventriloquized by SEYTON): To any prospective talent agents in the audience, I can guarantee satisfaction from this ventriloquist!
    
    \*SEYTON LAUGHS!\*
    
    SEYTON: DIE, MACBETH!
    
    \*SEYTON drags MACBETH offstage.\*
-
->->

=== heckled_outcome ===
// Hamlet play upstairs crashes the party
HECKLER (Offstage): Oh, boo! Boo Macbeth! You suck! Go feast on your own petards!

\*HAMLET, OPHELIA, KING CLAUDIUS, POLONIOUS, and LAERTES enter from the second floor, hitting the stage below.\*

DOCTOR (Offstage): Is everyone alright? I'm not a real doctor, but that seemed bad.

\*HAMLET stands up.\*

HAMLET: To be, or not to be, that is the question,
Whether 'tis nobler in the mind to suffer...

\*PLAY DIRECTOR enters.\*

PLAY DIRECTOR: What's going on here? We're in the middle of Macbeth!

\*HAMLET PLAY DIRECTOR enters from stage top.\*

HAMLET PLAY DIRECTOR: Well, we need a new venue, and ours just collapsed!

SEYTON: What kind of idiot builds two stories for one theatre? Isn't that just competing with your own business?

HAMLET PLAY DIRECTOR: Well, our version of Hamlet is populated entirely by formerly cursed goats and/or formerly cursed people, so that's quite a smaller demographic.

\*HAMLET PLAY DIRECTOR turns to the AUDIENCE.\*

HAMLET PLAY DIRECTOR: You're all welcome to join us upstairs by the way. We perform most weekday nights, and everyone is welcome, not just ex-cursees. We can also mostly guarantee that you will not be cursed during any of our performances, barring any witches in the audience or under the floorboards.

\*POLONIOUS bleats.\*

HAMLET: To-morrow and to-morrow, and to-morrow,
Creeps in this petty face,

HECKLER: Wait a second, that's Macbeth's soliloquy!

OPHELIA: Give it a rest, Hamlet!

PLAY DIRECTOR: HEY! You can't be promoting your own plays while we're in the middle of ours! And for the record, we DO have witches in the floorboards!

\*FIRST WITCH, SECOND WITCH, and THIRD WITCH enter from the floorboards.\*

FIRST WITCH: That's right! But we don't do curses, not without a nominal fee.

SECOND WITCH: The fee is actually quite expensive, but the first payment isn't! Then the APR increases drastically.

OPHELIA: Well, we can't just stop here, our audience from upstairs was looking forward to the conclusion!

\*UPSTAIRS AUDIENCE falls down onto the stage, and hurriedly sits \*

PLAY DIRECTOR: Hold on a second, if you were at the conclusion, then why is Hamlet giving a "To be or not to be" soliloquy, and why is Ophelia present at all? Aren't you supposed to be dead by ACT IV?

OPHELIA: SPOILERS! And I don't like being excluded!

HAMLET PLAY DIRECTOR: Ex-cursees don't like unhappy endings, so we typically razz and pizzazz things up a little to ensure good times for all.

HAMLET: Party rock is in the house tonight,
Everybody just have a good time.

PLAY DIRECTOR: That's complete crap! What is Shakespeare without a tragic ending? And spoilers? Who hasn't seen or read Hamlet yet?

\*HAMLET slumps over.\*

HAMLET: Oh, the poison must be kicking in now.

HAMLET PLAY DIRECTOR: Ohh, boo-hoo! Not every Shakespeare play has to be a tragedy. What about a Midsummer Night's Dream? And HAMLET, get up. You're not even poisoned yet, we still need to get to the fight with LAERTES!

LAERTES: Yeah, I'm standing right here, it's completely rude for you all to ignore me.

HAMLET: No, I took real poison. Little itty bitty doses of cyanide. I think they're just getting to me.

SEYTON (Shocked): WHAT? WHY!?

HAMLET: I am a serious method actor, and I need to sell my performance! If you all hadn't come in and screwed up everything, I'd be dying fighting Laertes right now!

\*QUEEN GERTRUDE enters from the top. She does not get back up.\*

HAMLET: Oh, the poison for the Queen must also be kicking in now.

OPHELIA (In Increasing Distress): I'm working with a psychopath! I'm working with a psychopath!

HAMLET PLAY DIRECTOR: Okay, use that Ophelia! That's the kind of motivation we're looking for. As for you Hamlet, I just explained, that WE ARE NOT DOING THE TRAGIC ENDINGS! What is wrong with you!?

HAMLET: I didn't read the scripts, okay!? I mean, I can't read, so I just had some dude I met at a bar give me a summary!

\*KING CLAUDIUS slumps over, and HAMLET struggles to stand up.\*

HAMLET: Oh crap, that was an incidental poisoning... sorry... King Claudius... I didn't know how to slip in the poison directly, so I just poisoned... like an entire section of the craft services table...

\*HAMLET goes flat.\*

HAMLET: ... and all the afterparty... booze... Bleh.

\*HAMLET dies.\*

HAMLET PLAY DIRECTOR: Okay, well now my principal actor is dead. So I'm going to need to borrow MACBETH... and the whole stage.

PLAY DIRECTOR: What? You can't do that!

HAMLET PLAY DIRECTOR: Oh yes I can. My finale involves a sword duel, don't you know. LAERTES!

\*LAERTES draws their sword.\*

LAERTES: We'll be taking this stage now.

PLAY DIRECTOR: MACBETH?

* MACBETH: TO ARMS! LET US DRIVE BACK THESE SWINE!
    \*The SOLDIERS draw wooden swords and approach the HAMLET actors.\*
    
    LAERTES: En garde!
    
    \*LAERTES pauses.\*
    
    LAERTES: Uhhh, wait a second. Does anyone else besides me have a sword?
    
    HAMLET PLAY DIRECTOR: HAMLET does!
    
    LAERTES: Hamlet just died.
    
    \*KING CLAUDIUS dies.\*
    
    LAERTES: So did King Clauidus. And Queen Gertrude.
    
    HAMLET PLAY DIRECTOR: Ah.
    
    \*LAERTES and HAMLET PLAY DIRECTOR look at each other.\*
    
    HAMLET PLAY DIRECTOR: Hamlet stars, REGROUP! WE'LL LIVE TO FIGHT ANOTHER DAY!
    
    \*LAERTES, HAMLET PLAY DIRECTOR, and OPHELIA exit.\*
    
    PLAY DIRECTOR: Alright, everyone back into positions! Let's just pretend this scene never happened.
    
    \*PLAY DIRECTOR, MACBETH, SEYTON, FIRST WITCH, SECOND WITCH, THIRD WITCH, and the SOLDIERS exit. QUEEN GERTRUDE, KING CLAUDIUS, and HAMLET remain on the stage for the duration of the play.\*
* MACBETH: Quick, in the confusion, someone kill Macduff!
    \*MACDUFF enters.\*
    
    MACDUFF: What?
    
    \*A SOLDIER breaks their wooden sword over MACDUFF's head. MACDUFF is unconscious. The rest of the SOLDIERS draw wooden swords and approach the HAMLET actors.\*
    LAERTES: En garde!
    
    \*LAERTES pauses.\*
    
    LAERTES: Uhhh, wait a second. Does anyone else besides me have a sword?
    
    HAMLET PLAY DIRECTOR: HAMLET does!
    
    LAERTES: Hamlet just died.
    
    \*KING CLAUDIUS dies.\*
    
    LAERTES: So did King Clauidus. And Queen Gertrude.
    
    HAMLET PLAY DIRECTOR: Ah.
    
    \*LAERTES and HAMLET PLAY DIRECTOR look at each other.\*
    
    HAMLET PLAY DIRECTOR: Hamlet stars, REGROUP! WE'LL LIVE TO FIGHT ANOTHER DAY!
    
    \*LAERTES, HAMLET PLAY DIRECTOR, and OPHELIA exit.\*
    
    PLAY DIRECTOR: Alright, everyone back into positions! Let's just pretend this scene never happened.
    
    \*OPHELIA enters.\*
    
    OPHELIA: Wait, you need a MACDUFF actor!
    
    PLAY DIRECTOR: No we don't...
    
    \*PLAY DIRECTOR notices MACDUFF's unconscious body.\*

    PLAY DIRECTOR: I guess we do. You're hired. You won't get paid anything, though.
    
    \*PLAY DIRECTOR, MACBETH, SEYTON, OPHELIA, FIRST WITCH, SECOND WITCH, THIRD WITCH, and the SOLDIERS exit. QUEEN GERTRUDE, KING CLAUDIUS, and HAMLET remain on the stage for the duration of the play.\*
    ~selfishMacduffReplacedByOphelia = true
    // Note, I left Polonious on the stage by accident, so I would absolutely 100% make use of that.
    // Oh, and a finale where everyone drinks the *poisoned* afterparty booze.
-
->->

=== surrendering_outcome ===
SEYTON: Ugh, here we ARE. Wait, weren't you surrendering to the English?

* MACBETH: Yes.
* MACBETH: No.

-
SEYTON: That's right, you were. Alright, Super Macbeth?

\*SUPER MACBETH enters.\*

SUPER MACBETH: Yes?

SEYTON: Dispose of Regular Macbeth.

SUPER MACBETH: Sure thing, boss.

\*SUPER MACBETH picks up MACBETH and piledrives MACBETH into the moon.\*

The End.

->END

=== waiting_for_death_outcome ===
\*PLAY DIRECTOR enters.\*

PLAY DIRECTOR: Okay, look, you're obviously going to just continue waiting for death by the hands of the English army. And that's no fun. So...

\*PLAY DIRECTOR turns to the AUDIENCE.\*
PLAY DIRECTOR: Alright everyone, are you ready for...

\*PLAY DIRECTOR pauses.\*

PLAY DIRECTOR: MAKING! NOISES!

\*PLAY DIRECTOR pauses.\*

PLAY DIRECTOR: It's more fun if you say the title while I'm saying it. Nevermind. Let's bring it out, shall we?

\*Two Stagehands wheel out a widescreen television.\*

PLAY DIRECTOR: Alright, and... go!

\*A Stagehand switches on the TV.\*

DOCTOR SIEVE: But Nurse Tourniquet! Don't you realize that my illegitimate child is your great nephew?

NURSE TOURNIQUET: Doctor! I thought we agreed to keep my intimacy problems our secret!

PLAY DIRECTOR: Alright, Macbeth?

* MACBETH: Wait, what's happening?
    DOCTOR SIEVE: Wait, what's happening?
* MACBETH: My brain has been mutated by some kind of viral fungus. Seek medical attention!
    DOCTOR SIEVE: My brain has been mutated by some kind of viral fungus. Seek medical attention!

-
PLAY DIRECTOR: Wow! What a performance!

NURSE TOURNIQUET: Why Doctor, you're terribly sick!

* MACBETH: My spleen is about to explode!
    DOCTOR SIEVE: Eat pig monkeys!
* MACBETH: Eat pig monkeys!
    DOCTOR SIEVE: My spleen is about to explode!
-
NURSE TOURNIQUET: But Doctor, you need those to live! I'll get a few doses of the good stuff, and we'll make sure to unendululuate you!

PLAY DIRECTOR: Okay, boring. Next thing.

\*A Stagehand clicks a few buttons on the TV.\*

TELEVISION MACBETH: To-morrow, and to-morrow, and to-morrow...

PLAY DIRECTOR: Oh hey, we can just use this instead of your crap performance. Alright everyone! Let's call it here.

And so they played Macbeth on TV and some of Grey's Anatomy until everyone got bored and went home.

The End.
->END

=== upset_seyton_outcome ===
SEYTON: Ahh, so we're doing this routine again. Don't worry Macbeth! I forgive VERY EASILY.

\*SEYTON escorts BRUTUS onstage.\*

SEYTON: Macbeth, meet Brutus. You're going to make things up to me by getting into Brutus' mouth.

\*SEYTON pauses.\*

SEYTON: Wait, maybe I should be clear. Brutus is a hippo. You're not climbing into the mouth of anything weird, believe me.

\*BRUTUS grunts.\*

SEYTON: Look, if it'll make you feel better, I'd be just as happy if you got under Brutus' feet.

* MACBETH: I'll stick with the mouth.
    SEYTON: Alright, great! Just hop on in there...
    
    \*MACBETH enters BRUTUS' mouth. MACBETH is crushed instantly in BRUTUS' powerful jaws.\*
    
* MACBETH: Feet! Feet!
    SEYTON: Alright, your funeral.
    
    \*MACBETH goes under BRUTUS' feet. MACBETH is crushed instantly.\*
-
SEYTON: Serves you right, you holistic wretch.

So MACBETH died. And there was much rejoicing!

The End.
->END

=== macduff_execution_outcome ===
SEYTON: Oh, we were executing Macduff, right?

\*MACDUFF enters, dragged by GUARDs.\*

MACDUFF: Hey wait a second, you can't do this! I'm supposed to kill you!

* MACBETH: Yes. Bring out the horses.
    \*Four horses enter. They are each fastened to a limb of MACDUFF.\*
    
    MACDUFF: Hey, wait a second! Is there some kind of safe word? This is really going off the rails, heh.
    
    SEYTON: Ready?
    
    MACDUFF: I'm pretty sure this is illegal. No, I'm definitely sure. I am definitely sure this is illegal. Please stop, please don't...
    
    SEYTON: Go!
* MACBETH: Oui oui. Bring out le guillotine.
    \*A Guillotine is wheeled out onto stage.\*

    MACDUFF: Hey, wait a second! Is there some kind of safe word? This is really going off the rails, heh.
    
    SEYTON: Alright, put your head in there.
    
    \*MACDUFF's head is placed in the guillotine.\*
    
    MACDUFF: I'm pretty sure this is illegal. No, I'm definitely sure. I am definitely sure this is illegal. Please stop, please don't...
    
    SEYTON: Go!
-
And so MACDUFF died. For real. And MACBETH went on to terrorize the rest of Scotland.

The End.
->END

=== selfish_end ===
    {
    - heckled_outcome:
        {selfishMacduffReplacedByOphelia:
            \*OPHELIA enters.\*
            OPHELIA: Alright, prepare to fight me, MACBETH! Because I am MACDUFF!
        }
        
        \*PLAY DIRECTOR enters.\*
        
        PLAY DIRECTOR: Sorry, I don't think I can do this anymore. MACBETH is probably just going to ruin this anyways. Maybe if you had made a concerted effort to learn your lines after this Hamlet disaster MACBETH, we might have all partied afterwards. But you didn't. So...
        
        \*POLONIOUS bleats.\*
        
        PLAY DIRECTOR: I agree, Goat POLONIOUS. Let's go home.
        
        And so the play got a bad review, and everyone was unhappy.
        
        The End.
    - left_early_outcome:
        \*SEYTON enters, and reattaches the stick to MACBETH.\*
        SEYTON: Alright, dummy, we're going to finish this play whether you like it or not.
        
        MACBETH (Ventriloquized by SEYTON): Sure thing, dummy!
        
        And so SEYTON finished the play. And everyone was really confused. One critic even called it "I don't know what happened, but I am certain I don't ever want to see this again."
        
        The End.
    - servant_murderer_outcome:
        \*SERVANT enters.\*
        
        SERVANT: Alright, ... maybe we should resolve this whole "trying to... kill me", thing.
        
        \*SEYTON enters.\*
        
        SEYTON: NO! DON'T! THAT COULD BRING THE BEES BACK!
        
        \*PLAY DIRECTOR enters.\*
        
        PLAY DIRECTOR: Yeah, let's just end this early so the bees...
        
        \*Humming noise begins again.\*
        
        PLAY DIRECTOR: Okay, theatre is dead.
        
        SEYTON: RUN!
        
        And so everyone got stung by bees, and the play got very negative reviews.
        
        The End.
    - crushed_under:
        \*MACBETH still mumbles, crushed underneath the stage light.\*
        
        \*MACDUFF enters.\*
    
        MACDUFF: Okay, prepare... to fight me, Macbeth.
        
        \*MACDUFF whacks their fake wooden sword against the stage light.\*
        
        {selfishDistinctOutcome == "MacduffHandsBound": 
            MACDUFF: This is especially difficult since my hands are bound, you know.
        -else:
            MACDUFF: Okay...
        }
        
        \*MACDUFF pauses.\*
        
        MACDUFF: Macbeth is dead, play over.
        
        And the play got resounding reviews, especially since Macbeth got crushed by a stagelight midway through. Huzzah!
    - selfishDistinctOutcome == "MacduffKilledKing" || selfishDistinctOutcome == "MacduffMurderedFather":
        \*MACDUFF enters.\*
        
        MACDUFF: Alright, let's just resolve this right now.
        
        {selfishDistinctOutcome == "MacduffKilledKing":
            MACDUFF: You think I killed the king...
        -else:
            MACDUFF: You think I killed your... father...
        }
        
        \*MACDUFF pauses.\*
        
        MACDUFF: Whatever, let's just end this early. En garde.
        
        \*MACDUFF draws their wooden sword, and whacks MACBETH over the head. MACBETH is knocked unconscious.\*
        
        And so the play was over, and everyone was kind of apathetic.
        
        The End.
    - selfishDistinctOutcome == "MacduffHandsBound":
        \*MACDUFF enters.\*
        
        MACDUFF: Uhhh, I guess my hands are bound? So... uh... I can't really kill you.
        
        \*MACDUFF pauses.\*
        
        MACDUFF: I give up, you win. Let's go home.
        
        The End.
    - else:
        ->selfish_end_no_distinct->
    }
-
->->

=== selfish_end_no_distinct ===
{selfishEnemy != "":
    \*{selfishEnemy} enters.\*
    {print_enemy("n", "N")}ot this again...
}

{selfishEnemy != "SEYTON":
    \*SEYTON enters.\*
}

SEYTON: You know what? We're at the end anyways. Let me handle this.

\*SEYTON walks towards MACBETH.\*

SEYTON: To be, or not to be...

\*SEYTON trips on a loose floorboard.\*

SEYTON: Whoops!

And so Seyton tripped, absolutely ruining the play. And everyone left super unsatisfied.

The End.
->->